external ext_length // Calculates the length of a list
external ext_pop	// Pop the first element of a list
external ext_isEmpty // Returns a bool indicating an empty list if true
external ext_print 	// Prints a message
external ext_avg	// Calculates the average of a list of values

connectionConfig EdgeWifi {
	type:WLAN
	"ssid":"CableBox-9655-2_4Ghz"
	"password": "connectnow20"
}

edge device LightSensor {
	setup EdgeWifi
	
	list lightLevels
	var lightLevel
	var lower = 90
	var upper = 300
	var isEmpty = true
	var NIL
	
	/*
	 * Read light level loop
	 */
	every 1 SECONDS {
		read from LIGHTSENSOR -> to lightLevel
		ext_print of lightLevel -> to NIL
		
		// Record values that are exceeding boundaries
		if lightLevel > lower && lightLevel < upper{
			read var lightLevel -> add to lightLevels
		}
	}
	
	/*
	 * Send to fog controller loop
	 */
	always {
		ext_isEmpty of lightLevels -> to isEmpty
		
		if isEmpty == false {
			LED ON // Indicate network activity
			ext_pop of lightLevels -> send to FogController
			LED OFF
		}		
	}
}

fog device FogController {
	
	list lightLevels
	var listLength
	var threshold = 10
	var lightLevelAvg
	var NIL
	
	/*
	 * Setup server for communicating with edge-nodes
	 */
	listen on 192.168.0.3:8123 -> add to lightLevels
	
	
	/*
	 * Thread that calculates avg and sends it to the cloud
	 */
	always {
		ext_length of lightLevels -> to listLength
		
		if listLength >= threshold {
			ext_print of listLength -> to NIL
			ext_avg of lightLevels -> to lightLevelAvg
			read var lightLevelAvg -> send to CloudService
			clear lightLevels
		}
	}
}


cloud device CloudService {
	
	/*
	 * Setup server for communicating with fog-nodes
	 */
	listen on 192.168.0.3:8124 -> ext_print of value
}