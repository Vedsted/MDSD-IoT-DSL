grammar sdu.mdsd.IoT with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate ioT "http://www.mdsd.sdu/IoT"

Model:
	deviceTypes+=DeviceDefinition*
	system=IoTSystem?;

DeviceDefinition:
	'devicetype' name=ID fileName=STRING ':'
	body=STRING
	implementations+=Implementation*;

Implementation:
	name=ID '(' params+=Parameter ( ',' params+=Parameter)* ')' ':'
	body=ImplBody
;

Parameter:
	name=ID (isArray?='[]')?
;
	
ImplBody:
	{ImplBody} 
	('import:' imports=STRING)?
	('setup:'setup=STRING)?
	('use:' use=STRING)?
	;

IoTSystem:
	devices+=Device+;

Device:
	deviceType=[DeviceDefinition] 'device' name=ID '{'
	program=Program
	'}';

Program:
	{Program} topLevelCommands+= TopLevelCommand*
	
;

TopLevelCommand:
	Command | Number | StringValue
;

StringValue:
	value=STRING
;

Number:
	value=INT
;

Argument:
	param=[Parameter] '=' (value=TopLevelCommand | '[' values+=TopLevelCommand (',' values+=TopLevelCommand)* ']')
;

Command:
	template=[Implementation] '('args+=Argument (',' args+=Argument )* ')'
;



